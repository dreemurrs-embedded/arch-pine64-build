# Maintainer: realroot <scorpion2185@protonmail.com>
# Based on AUR package of: Gerrit G. <gerrit at grosskopfgames dot de>
pkgname=unl0kr
pkgver=1.0
pkgdesc="On-screen Keyboard for FDE"
pkgrel=1
arch=(x86_64 aarch64)
url="https://github.com/Grosskopf/arch_unl0kr"
license=('GPL3')
depends=(device-mapper cryptsetup mesa libinput libxkbcommon)
makedepends=(meson)
_commit="50b6a2373679d09c9bf59eb1933cf14c16428ed0"
source=(
    git+https://gitlab.com/cherrypicker/unl0kr.git#commit=${_commit}
    unl0kr-hooks
    unl0kr-install
)
build() {
    cd unl0kr
    git submodule init
    git submodule update
    cd ..
    arch-meson "$pkgname" _build
    meson compile -C _build
}
package() {
    DESTDIR="$pkgdir" meson install --no-rebuild -C _build
    install -Dm644 ${srcdir}/unl0kr-hooks ${pkgdir}/usr/lib/initcpio/hooks/unl0kr
    install -Dm644 ${srcdir}/unl0kr-install ${pkgdir}/usr/lib/initcpio/install/unl0kr
    sed -i 's/default=breezy-light/default=breezy-dark/' ${pkgdir}/etc/unl0kr.conf
    sed -i 's/alternate=breezy-dark/alternate=breezy-light/' ${pkgdir}/etc/unl0kr.conf
}
sha256sums=('SKIP'
            '8e78f15625b3caafa75e698f711ebc62f0a595a714f87f17b1b17299ecc98e04'
            '41dfe0246fd5ff644e3b46c46c2b0e57dda431446d658c3b0534eb25afd021c6')
